/*--------------- 1ra Area: Codigo de Usuario -----------------------*/
//-------> importaciones, paquetes

package gnz.backend.analizadores;
import java_cup.runtime.*;
import gnz.backend.nodo.Nodo;
import gnz.backend.nodoExpresion.*;
import gnz.backend.nodoComparacion.*;
import gnz.backend.nodoDeclaracion.*;
import gnz.gui.frames.*;
import java.util.LinkedList;

parser code {:

    private EditorDeTextoFrame editor;

    public parser(AnalizadorLexicoCodigo lex,EditorDeTextoFrame editor){
        super(lex);
        this.editor=editor;
    }

    //Metodo al que se llama automaticamente ante algun error sintactico
    public void syntax_error(Symbol s){
        int columna = s.right;
        int linea = s.left;
        System.out.println("Error SINTACTICO:" + s.value + " en:" + linea + ":" + columna);
}

    //Metodo al que se llama en el momento en que ya no es posible una recuperacion de errores
    public void unrecovered_syntax_error(Symbol s) throws java.lang.Exception{
        int columna = s.right;
        int linea = s.left;
        System.out.println("Error SINTACTICO:" + s.value + " en:" + linea + ":" + columna);
 }


:}

/*--------------- 2da Area: Declaraciones -----------------------*/

terminal String     PCOMA;
terminal String     ASIGNACION,COMA;
terminal String     IDENTIFICADOR;
terminal String     BOOLEAN;
terminal String     TRUE,FALSE;
terminal String     CHAR;
terminal String     DECLARACION_CARACTER;
terminal String     BYTE,INT,LONG,FLOAT,DOUBLE;
terminal String     NUMERO_ENTERO,NUMERO_DECIMAL,NUMERO_DECIMALF;
terminal String     STRING;
terminal String     DECLARACION_STRING;

terminal String     MAS,POR,MENOS,DIV,PORCENTAJE;
terminal String     MENOR,MAYOR,MENOR_IGUAL,MAYOR_IGUAL,DISTINTO_DE,IGUAL_A;
terminal String     NOT,AND,OR;

terminal String     PARENTESIS_ABIERTO,PARENTESIS_CERRADO,CORCHETE_ABIERTO,CORCHETE_CERRADO;

terminal String     IF,ELSIF,ELSE;

terminal String     WHILE,DO;

terminal String     FOR,DOS_PUNTOS;

terminal String     ARRAY;

terminal String     PRINT,PRINTLN,SCANS,SCANN;

terminal String     VOID,RETURN;

terminal String     MAIN;

//terminal String     COMENTARIO_LINEA,COMENTARIO_DE_BLOQUE;


non terminal Object     inicio;
non terminal Object     conjuntoDeInstrucciones;
non terminal Nodo       unaInstruccion;
non terminal LinkedList<Nodo>     conjuntoDeDeclaraciones;
non terminal Nodo     unaDeclaracion;


//******************************************************************Declaracion de variables******************************************

non terminal Nodo     declaracionDeVariables;
non terminal TipoDeVariable     tipoDeVariable;
non terminal LinkedList<Nodo>     conjuntoDeIdentificadoresParaVariables;
non terminal Nodo     unIdentificadorParaVariable;
non terminal Nodo     expresion;

//******************************************************************Condicion y comparacion******************************************

non terminal Nodo     comparacion;
non terminal Object     condicion;


//******************************************************************Asignacion de variables******************************************

non terminal Object     asignacionDeVariable;

//******************************************************************Declaracion de arreglos******************************************

non terminal Object     declaracionDeArreglos;
non terminal Object     unIdentificadorParaArreglo;
non terminal Object     conjuntoDeDimensiones;
non terminal Object     unaDimension;
non terminal Object     conjuntoDeIdentificadoresParaArreglo;
non terminal Object     asignacionDeArreglo;

//******************************************************************Declaracion de estructura If******************************************

non terminal Object     declaracionDeEstructuraIf;

//******************************************************************Declaracion de estructura IF-ELSE******************************************

non terminal Object     declaracionDeEstructuraIfElse;

//******************************************************************Declaracion de estructura ELSEIF******************************************

non terminal Object     declaracionDeEstructuraElseIf,conjuntoDeElseIf,unElseIf;


//******************************************************************DeclaracionCHAR de estructura While******************************************

non terminal Object     declaracionDeEstructuraWhile;
non terminal Object     instrucccionWhile;

//******************************************************************declaracion de estructura DoWhile********************************************

non terminal Object     declaracionDeEstructuraDoWhile;
non terminal Object     declaracionDo;

//******************************************************************declaracion de estructura For********************************************

non terminal Object     declaracionDeEstructuraFor,incrementoDecremento;
non terminal Object     asignacionParaFor;

//******************************************************************declaracion de funciones Predefinidas********************************************

non terminal Object     declaracionDeFuncionesPredefinidas;
non terminal Object     conjuntoDeTexto;
non terminal Object     unTexto;

//***********************************************************************************Subprogramas*****************************************************

non terminal Object     conjuntoDeSubprogramas;
non terminal Object     unSubPrograma;
non terminal Object     tipoDeRetorno;
non terminal Object     conjuntoDeParametros;
non terminal Object     unParametro;
non terminal Object     cuerpoDeFuncion,cuerpoDeFuncionReturn;

//***********************************************************************************Funcion Main***************************************************
non terminal Object funcionMain;


/*--------------- 3ra Area: Reglas Semanticas -----------------------*/

precedence left ELSE;
precedence left ELSIF;
precedence left MAS, MENOS;
precedence left POR, DIV,PORCENTAJE;
precedence left NOT,AND,OR;

start with inicio;
//*******************************************************Expresiones,comparaciones y tipos de variables****************************
expresion::= expresion:n1 MAS:i expresion:n2{:RESULT= new NodoExpresion(OperacionAritmetica.MAS,n1,n2,ileft,iright);:};
expresion::= expresion:n1 MENOS:i expresion:n2{:RESULT= new NodoExpresion(OperacionAritmetica.MENOS,n1, n2,ileft,iright);:};
expresion::=expresion:n1 POR:i expresion:n2{:RESULT= new NodoExpresion(OperacionAritmetica.POR,n1,n2,ileft,iright);:};
expresion::=expresion:n1 DIV:i expresion:n2{:RESULT= new NodoExpresion(OperacionAritmetica.DIV,n1,n2,ileft,iright);:};
expresion::=expresion:n1 PORCENTAJE:i expresion:n2{:RESULT= new NodoExpresion(OperacionAritmetica.PORCENTAJE,n1,n2,ileft,iright);:};
expresion::=PARENTESIS_ABIERTO expresion:n1 PARENTESIS_CERRADO{:RESULT = n1;:};
expresion::=MENOS PARENTESIS_ABIERTO expresion PARENTESIS_CERRADO;

expresion::=IDENTIFICADOR:i{:RESULT=new NodoHojaExpresion(TipoDeHoja.IDENTIFICADOR,i,ileft,iright);:};
expresion::=TRUE:i{:RESULT=new NodoHojaExpresion(TipoDeHoja.TRUE,i,ileft,iright);:};
expresion::=FALSE:i{:RESULT=new NodoHojaExpresion(TipoDeHoja.FALSE,i,ileft,iright);:};
expresion::=DECLARACION_CARACTER:i{:RESULT=new NodoHojaExpresion(TipoDeHoja.DECLARACION_CARACTER,i,ileft,iright);:};
expresion::=NUMERO_ENTERO:i{:RESULT=new NodoHojaExpresion(TipoDeHoja.NUMERO_ENTERO,i,ileft,iright);:};
expresion::=NUMERO_DECIMAL:i{:RESULT=new NodoHojaExpresion(TipoDeHoja.NUMERO_DECIMAL,i,ileft,iright);:};
expresion::=NUMERO_DECIMALF:i{:RESULT=new NodoHojaExpresion(TipoDeHoja.NUMERO_DECIMALF,i,ileft,iright);:};
expresion::=DECLARACION_STRING:i{:RESULT=new NodoHojaExpresion(TipoDeHoja.DECLARACION_STRING,i,ileft,iright);:};


comparacion::= comparacion:n1 AND comparacion:n2{:RESULT = new NodoLogico(OperacionLogica.AND,n1,n2);:};
comparacion::= comparacion:n1 OR comparacion:n2{:RESULT = new NodoLogico(OperacionLogica.OR,n1,n2);:};
comparacion::= NOT comparacion:n1;
//comparacion::= PARENTESIS_ABIERTO comparacion PARENTESIS_CERRADO;
comparacion::=expresion:n1 MENOR expresion:n2{:RESULT= new NodoComparacion(OperacionComparacion.MENOR,n1,n2);:};
comparacion::=expresion:n1 MAYOR expresion:n2{:RESULT= new NodoComparacion(OperacionComparacion.MAYOR,n1,n2);:};
comparacion::=expresion:n1 MENOR_IGUAL expresion:n2{:RESULT= new NodoComparacion(OperacionComparacion.MENOR_IGUAL,n1,n2);:};
comparacion::=expresion:n1 MAYOR_IGUAL expresion:n2{:RESULT= new NodoComparacion(OperacionComparacion.MAYOR_IGUAL,n1,n2);:};
comparacion::=expresion:n1 DISTINTO_DE expresion:n2{:RESULT= new NodoComparacion(OperacionComparacion.DISTINTO_DE,n1,n2);:};
comparacion::=expresion:n1 IGUAL_A expresion:n2{:RESULT= new NodoComparacion(OperacionComparacion.IGUAL_A,n1,n2);:};
comparacion::=expresion:n1 {:RESULT=n1;:};

tipoDeVariable::=STRING{:RESULT=TipoDeVariable.STRING;:};
tipoDeVariable::=BOOLEAN{:RESULT=TipoDeVariable.BOOLEAN;:};
tipoDeVariable::=CHAR{:RESULT=TipoDeVariable.CHAR;:};
tipoDeVariable::=BYTE{:RESULT=TipoDeVariable.BYTE;:};
tipoDeVariable::=INT{:RESULT=TipoDeVariable.INT;:};
tipoDeVariable::=LONG{:RESULT=TipoDeVariable.LONG;:};
tipoDeVariable::=FLOAT{:RESULT=TipoDeVariable.FLOAT;:};
tipoDeVariable::=DOUBLE{:RESULT=TipoDeVariable.DOUBLE;:};


//******************************************************************Inicio**********************************************************

inicio::=conjuntoDeDeclaraciones:declaraciones
{:ManejadorDeDeclaraciones man = new ManejadorDeDeclaraciones(declaraciones,editor);
:};
inicio::= conjuntoDeDeclaraciones conjuntoDeSubprogramas PORCENTAJE PORCENTAJE PORCENTAJE funcionMain;
inicio::= conjuntoDeDeclaraciones  PORCENTAJE PORCENTAJE PORCENTAJE funcionMain;
inicio::= conjuntoDeSubprogramas  PORCENTAJE PORCENTAJE PORCENTAJE funcionMain;
inicio::= PORCENTAJE PORCENTAJE PORCENTAJE funcionMain;


conjuntoDeDeclaraciones::=conjuntoDeDeclaraciones:lista unaDeclaracion:n1
{:lista.add(n1);RESULT=lista;:}
 |
 unaDeclaracion:n1
 {:LinkedList<Nodo> lista = new LinkedList<>();
   lista.add(n1);
   RESULT=lista;
 :};
unaDeclaracion::=declaracionDeVariables:n1{:RESULT=n1;:};
unaDeclaracion::=declaracionDeArreglos;

conjuntoDeInstrucciones::=conjuntoDeInstrucciones unaInstruccion | unaInstruccion;

//******************************************************************Error**********************************************************

unaInstruccion::=error;

//******************************************************************Declaracion de variables****************************************************
unaInstruccion::=declaracionDeVariables:n1{:RESULT =n1;:};
declaracionDeVariables::=tipoDeVariable:tipo conjuntoDeIdentificadoresParaVariables:lista PCOMA
{:RESULT = new NodoDeclaracion(tipo,lista);:};
conjuntoDeIdentificadoresParaVariables::=
conjuntoDeIdentificadoresParaVariables:lista COMA unIdentificadorParaVariable:n1
{:lista.add(n1);RESULT = lista;:}
  |
unIdentificadorParaVariable:n1
{:LinkedList<Nodo> miLista = new LinkedList<>();
  miLista.add(n1);
  RESULT= miLista;
:};

unIdentificadorParaVariable::=IDENTIFICADOR:i {:RESULT= new NodoId(i,null,ileft,iright);:};
unIdentificadorParaVariable::=IDENTIFICADOR:i ASIGNACION comparacion:n1 {:RESULT= new NodoId(i,n1,ileft,iright);:};

//******************************************************************Asignacion de variables****************************************************
unaInstruccion::=asignacionDeVariable;
asignacionDeVariable::=IDENTIFICADOR ASIGNACION comparacion PCOMA;

//*******************************************************************Declaracion de arreglos********************************************
unaInstruccion::=declaracionDeArreglos;
declaracionDeArreglos::=ARRAY tipoDeVariable conjuntoDeDimensiones conjuntoDeIdentificadoresParaArreglo PCOMA;
conjuntoDeDimensiones::=conjuntoDeDimensiones unaDimension|unaDimension;
unaDimension::=CORCHETE_ABIERTO comparacion CORCHETE_CERRADO;
conjuntoDeIdentificadoresParaArreglo::=conjuntoDeIdentificadoresParaArreglo COMA unIdentificadorParaArreglo | unIdentificadorParaArreglo;
unIdentificadorParaArreglo::=IDENTIFICADOR;

//*******************************************************************Asignacion de arreglos********************************************
unaInstruccion::=asignacionDeArreglo;
asignacionDeArreglo::=IDENTIFICADOR conjuntoDeDimensiones ASIGNACION comparacion PCOMA;



//*******************************************************************Instruccion IF ***************************************************
unaInstruccion::=declaracionDeEstructuraIf;
//------>1
declaracionDeEstructuraIf::= IF PARENTESIS_ABIERTO comparacion PARENTESIS_CERRADO unaInstruccion;
//------>2
declaracionDeEstructuraIf::= IF PARENTESIS_ABIERTO comparacion PARENTESIS_CERRADO CORCHETE_ABIERTO conjuntoDeInstrucciones CORCHETE_CERRADO;
//*****************************************************************Instruccion IF-ELSE*****************************************************
unaInstruccion::=declaracionDeEstructuraIfElse;
//------->1
declaracionDeEstructuraIfElse::= declaracionDeEstructuraIf ELSE unaInstruccion;
//------->2
declaracionDeEstructuraIfElse::= declaracionDeEstructuraIf ELSE CORCHETE_ABIERTO conjuntoDeInstrucciones CORCHETE_CERRADO;
//****************************************************************Instruccion ELSEIF*******************************************************
unaInstruccion::=declaracionDeEstructuraElseIf;
//------->1
declaracionDeEstructuraElseIf::=declaracionDeEstructuraIf conjuntoDeElseIf;
//------->2
declaracionDeEstructuraElseIf::=declaracionDeEstructuraIf conjuntoDeElseIf ELSE unaInstruccion;
//------->3
declaracionDeEstructuraElseIf::=declaracionDeEstructuraIf conjuntoDeElseIf ELSE CORCHETE_ABIERTO conjuntoDeInstrucciones CORCHETE_CERRADO;

conjuntoDeElseIf::=conjuntoDeElseIf unElseIf|unElseIf;
unElseIf::=ELSIF PARENTESIS_ABIERTO comparacion PARENTESIS_CERRADO unaInstruccion;
unElseIf::=ELSIF PARENTESIS_ABIERTO comparacion PARENTESIS_CERRADO CORCHETE_ABIERTO conjuntoDeInstrucciones CORCHETE_CERRADO;


//**********************************************************Instruccion While*************************************************************
unaInstruccion::=declaracionDeEstructuraWhile;
declaracionDeEstructuraWhile::= WHILE PARENTESIS_ABIERTO comparacion PARENTESIS_CERRADO unaInstruccion;
declaracionDeEstructuraWhile::= WHILE PARENTESIS_ABIERTO comparacion PARENTESIS_CERRADO CORCHETE_ABIERTO conjuntoDeInstrucciones CORCHETE_CERRADO;

//*********************************************************Instruccion Do-While***********************************************************
unaInstruccion::=declaracionDeEstructuraDoWhile;
declaracionDeEstructuraDoWhile::= DO unaInstruccion WHILE PARENTESIS_ABIERTO comparacion PARENTESIS_CERRADO;
declaracionDeEstructuraDoWhile::= DO CORCHETE_ABIERTO conjuntoDeInstrucciones CORCHETE_CERRADO WHILE PARENTESIS_ABIERTO comparacion PARENTESIS_CERRADO;
//*********************************************************Instruccion For*****************************************************************
unaInstruccion::=declaracionDeEstructuraFor;
declaracionDeEstructuraFor::=FOR PARENTESIS_ABIERTO asignacionParaFor DOS_PUNTOS comparacion DOS_PUNTOS incrementoDecremento PARENTESIS_CERRADO unaInstruccion;
declaracionDeEstructuraFor::=FOR PARENTESIS_ABIERTO asignacionParaFor DOS_PUNTOS comparacion DOS_PUNTOS incrementoDecremento PARENTESIS_CERRADO
                             CORCHETE_ABIERTO conjuntoDeInstrucciones CORCHETE_CERRADO;
asignacionParaFor::=IDENTIFICADOR ASIGNACION comparacion;
incrementoDecremento::= MAS NUMERO_ENTERO;
incrementoDecremento::= MENOS NUMERO_ENTERO;
//*********************************************************Funciones predefinidas************************************************************
unaInstruccion::=declaracionDeFuncionesPredefinidas;
declaracionDeFuncionesPredefinidas::= PRINTLN CORCHETE_ABIERTO conjuntoDeTexto CORCHETE_CERRADO PCOMA;
declaracionDeFuncionesPredefinidas::= PRINT CORCHETE_ABIERTO conjuntoDeTexto CORCHETE_CERRADO PCOMA;
declaracionDeFuncionesPredefinidas::= SCANS CORCHETE_ABIERTO IDENTIFICADOR CORCHETE_CERRADO PCOMA;
declaracionDeFuncionesPredefinidas::= SCANN CORCHETE_ABIERTO IDENTIFICADOR CORCHETE_CERRADO PCOMA;
conjuntoDeTexto::= conjuntoDeTexto COMA unTexto | unTexto;
unTexto::=expresion;

//*********************************************************Subprogramas**********************************************************************

conjuntoDeSubprogramas::= conjuntoDeSubprogramas unSubPrograma| unSubPrograma;
unSubPrograma::=tipoDeVariable IDENTIFICADOR PARENTESIS_ABIERTO conjuntoDeParametros PARENTESIS_CERRADO
                CORCHETE_ABIERTO cuerpoDeFuncionReturn CORCHETE_CERRADO;

unSubPrograma::=VOID IDENTIFICADOR PARENTESIS_ABIERTO conjuntoDeParametros PARENTESIS_CERRADO
                                CORCHETE_ABIERTO cuerpoDeFuncion CORCHETE_CERRADO;


conjuntoDeParametros::= conjuntoDeParametros COMA unParametro | unParametro |;
unParametro::= tipoDeVariable IDENTIFICADOR;

cuerpoDeFuncionReturn::=conjuntoDeDeclaraciones PORCENTAJE PORCENTAJE PORCENTAJE conjuntoDeInstrucciones RETURN comparacion;
cuerpoDeFuncionReturn::=conjuntoDeDeclaraciones PORCENTAJE PORCENTAJE PORCENTAJE RETURN comparacion;
cuerpoDeFuncionReturn::= PORCENTAJE PORCENTAJE PORCENTAJE conjuntoDeInstrucciones RETURN comparacion;
cuerpoDeFuncionReturn::= PORCENTAJE PORCENTAJE PORCENTAJE RETURN comparacion;


cuerpoDeFuncion::=conjuntoDeDeclaraciones PORCENTAJE PORCENTAJE PORCENTAJE conjuntoDeInstrucciones;
cuerpoDeFuncion::=conjuntoDeDeclaraciones PORCENTAJE PORCENTAJE PORCENTAJE;
cuerpoDeFuncion::= PORCENTAJE PORCENTAJE PORCENTAJE conjuntoDeInstrucciones;
conjuntoDeInstrucciones::=PORCENTAJE PORCENTAJE PORCENTAJE;

//***************************************************************funcionMain************************************************************
funcionMain::= MAIN PARENTESIS_ABIERTO PARENTESIS_CERRADO CORCHETE_ABIERTO conjuntoDeInstrucciones CORCHETE_CERRADO;
